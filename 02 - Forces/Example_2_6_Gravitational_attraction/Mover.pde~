

class Mover
{

  PVector location;
  PVector velocity;
  PVector acceleration;
  // The object now has mass!
  float mass;
  float x, y;
  float G;
  PFont font;
  
  Mover(float _m, float _x, float _y, PFont _f)
  {
    // And for now, we’ll just set the mass equal to 1 for simplicity.
    mass = _m;
    x = _x;
    y = _y;
    location = new PVector(x,y);
    velocity = new PVector(0,0);
    acceleration = new PVector(0,0);
    G = 0.7;
    font = _f;
  }

  // Newton’s second law.
  void applyForce(PVector force)
  {
    // Receive a force, divide by mass, and add to acceleration.
    PVector f = PVector.div(force,mass);
    acceleration.add(f);

  }

  void update()
  {
    // Motion 101 from Chapter 1
    velocity.add(acceleration);
    location.add(velocity);
    acceleration.mult(0);
  }

  void display()
  {
    stroke(0);
    fill(0,100, 225);
    // Scaling the size according to mass.
    ellipse(location.x,location.y,mass*16,mass*16);
    fill(0);
    textAlign(CENTER);
    text(velocity.mag(), location.x, location.y + textAscent()/2);
  }

  PVector attract(Attractor a)
  {

    PVector force = PVector.sub(location, a.location); //get the vector difference between two points
    float distance = force.mag(); //get the scalar distance between the two points (the magnitude of the vector)

    distance = constrain(distance,5.0,25.0); //constrain the distance, partly to avoid division by zero
    //but also because extreme values look odd in a processing sketch
    
    force.normalize(); //normalize the vector difference between the two points;
    //now it's just a direction unit vector

    float strength = (G * mass * a.mass) / (distance * distance); //compute strength of the attraction as a scalar
    force.mult(strength); //multiply the unit vector by the calculated scalar to give the force as a vector
    return force;
  }

  

  // Somewhat arbitrarily, we are deciding that an object bounces when it hits the edges of a window.
  void checkEdges()
  {
    if (location.x > width)
    {
      location.x = width;
      velocity.x *= -1;
    }
    else if (location.x < 0)
    {
      velocity.x *= -1;
      location.x = 0;
    }

    if (location.y > height)
    {
      // Even though we said we shouldn't touch location and velocity directly,
      // there are some exceptions.
      // Here we are doing so as a quick and easy way to reverse the direction
      // of our object when it reaches the edge.
      velocity.y *= -1;
      location.y = height;
    }
    else if (location.y < 0)
    {
      velocity.y *= -1;
      location.y = 0;
    }
  }
}
